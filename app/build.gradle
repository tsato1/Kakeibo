apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    def versionMajor = 3
    def versionMinor = 0
    def versionDB = 6

    defaultConfig {
        applicationId "com.kakeibo"
        minSdkVersion 22
        targetSdkVersion 29

        versionCode 24
        versionName "${versionMajor}.${versionMinor}.${versionDB}"
        /*** major.minor.db // minor:0=basic, 100=plus, 1000=plus plus ***/

        multiDexEnabled true

        applicationVariants.all { variant ->
            variant.resValue "string", "versionName", "v: "+variant.versionName
        }

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        buildTypes.each {
            it.buildConfigField('int', 'versionMajor', "${versionMajor}")
            it.buildConfigField('int', 'versionDB', "${versionDB}")
        }
        release {
            minifyEnabled true  // shrink
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
        baseline file("lint-baseline.xml")
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions "default"
    productFlavors {
        free {
            buildConfigField('int', 'versionMinor', "${versionMinor}")
        }
        paid {
            buildConfigField('int', 'versionMinor', "${versionMinor}+100")
            applicationIdSuffix ".paid"
            versionNameSuffix "-paid"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.android.gms:play-services-drive:17.0.0'
    implementation 'com.google.android.gms:play-services-ads:18.3.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:17.0.0'
    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.google.firebase:firebase-ads:18.3.0'
    implementation 'com.google.firebase:firebase-auth:19.2.0'
    implementation 'com.google.firebase:firebase-analytics:17.2.1'
    implementation 'pub.devrel:easypermissions:0.3.0'
    implementation 'com.adcolony:sdk:4.1.0'
    implementation('com.google.api-client:google-api-client-android:1.23.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-sheets:v4-rev35-1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation project(':HoloGraphLibrary')
    implementation project(':DynamicGrid')
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'


    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha03'
}

apply plugin: 'com.google.gms.google-services'